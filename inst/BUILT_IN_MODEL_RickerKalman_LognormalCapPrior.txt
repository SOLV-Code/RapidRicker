model{
	# adapted from code by Ann-Marie Huang, which was originally contributed by Catherine Michielsens
		# modifications based on comments and code samples from Ann-Marie Huang, Brendan Connors, Charmaine
	# Carr-Harris, and Wendell Challenger.


    for (i in 1:N) {                       #loop over N sample points
      R_Obs[i] ~ dlnorm(logR[i],tau_R)          #likelihood -> predicted value for NA in data set
      logR[i] <- RS[i] +log(S[i])               # calc log(R) - fitted values
      RS[i] <- ln.alpha[i] - beta * S[i]           # ricker model
	  year[i]<-i
	  log.resid[i] <-  log(R_Obs[i]) - logR[i]  
	}

    for (i in 2:N){
          ln.alpha[i] <- ln.alpha[i-1] + w[i]
          w[i]~ dnorm(0,tauw)
    }



    ln.alpha[1] ~ dnorm(p.alpha,tau_alpha)        #prior for alpha (actually ln.alpha!)
    beta <-1/ S.max					   # prior for beta
	
	S.max ~ dlnorm(log(smax.in), tau_smax) T(0,smax.limit)
	smax.limit <- max.scalar * smax.in # typical default  3 * (Max Obs)

	# non-updating samples (so can plot priors)
	S.max.prior ~ dlnorm(smax.in, tau_smax) T(0,smax.limit)
	ln.alpha.prior ~ dnorm(p.alpha,tau_alpha)
	
    tau_R ~ dgamma(shape.tau_R,lambda_tau_R)                    #prior for precision parameter
    sigma <- 1/sqrt(tau_R) 			# based on Fleishman and Evenson

	tauw~ dgamma(shape.tauw,lambda_tauw)
    varw<- 1/tauw
	sigw<- 1/sqrt(tauw)


    for (i in 1:N) {  
			ln.alpha.c[i] <- ln.alpha[i] + (sigma * sigma / 2) # bias correction for lognormal skewness
	}
	
}
