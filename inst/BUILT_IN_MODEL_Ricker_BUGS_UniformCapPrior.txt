model{
	# adapted from code originally developed by Catherine Michielsens, Sue Grant, and Bronwyn MacDonald.
	# modified to incorporate Smax upper constraint (and replaced C with Smax)
	# modified to focus on SR parameters only. BM calcs done separately
	# NOTE: THIS VERSION DOES THE BIAS CORRECTION OUTSIDE THE LIKELIHOOD, 
	#       AND IT TRACKS BOTH VERSIONS OF LN.ALPHA (last line of code)

    for (i in 1:N) {                       #loop over N sample points
      R_Obs[i] ~ dlnorm(logR[i],tau_R)          #likelihood -> predicted value for NA in data set
      logR[i] <- RS[i] +log(S[i])               # calc log(R) - fitted values
      RS[i] <- ln.alpha - beta * S[i]           # ricker model
	  
	  log.resid[i] <-  log(R_Obs[i]) - logR[i] 
	  
   }

    ln.alpha ~ dnorm(p.alpha,tau_alpha)  #prior for lnalpha 
    beta <- 1/ S.max				     # prior for beta

    S.max ~ dunif(1/10^6, max.scalar * S.max.in )  # data is in millions

	
	# non-updating samples (so can plot priors)
	S.max.prior ~ dunif(1/10^6, max.scalar * S.max.in)
	ln.alpha.prior ~ dnorm(p.alpha,tau_alpha)
	
    tau_R ~ dgamma(shape.tau_R,lambda_tau_R)             #prior for precision parameter
    sigma <- 1/sqrt(tau_R) 			# changed based on Fleishman and Evenson

	ln.alpha.c <- ln.alpha + (sigma * sigma / 2) # bias correction for lognormal skewness

}
